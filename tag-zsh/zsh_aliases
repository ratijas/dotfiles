# General zsh utils
alias zshconfig="$VISUAL $HOME/.zshrc"
alias zshaliases="$VISUAL $HOME/.zsh_aliases"
alias k="k -h"
alias :q="exit"

# Prevent self-harm
# https://twitter.com/ns_schoe/status/1145741477424881666
alias cp='cp -i'
alias rm='rm -i'
alias crontab='crontab -i'

# Sailfish SDK
# function ssh-sailfish() { ssh -p "$1" -i /opt/SailfishOS/SDK/vmshare/ssh/private_keys/"$2"/"$3" "$3"@localhost }
# alias ssh-sailfish-emu-nemo="ssh-sailfish 2223 SailfishOS_Emulator nemo"
# alias ssh-sailfish-emu-root="ssh-sailfish 2223 SailfishOS_Emulator root"
# alias ssh-sailfish-sdk-mer=" ssh-sailfish 2222 engine mersdk"
# alias ssh-sailfish-sdk-root="ssh-sailfish 2222 engine root"

# Uncategorized
alias /autism="pacaur -Suy"
alias whatismyip="wget -4qO- http://whatismyip.akamai.com/"
alias urldecode='python -c "import sys; from urllib.parse import unquote_plus as up; sys.stdout.write(up(sys.stdin.read()))"'
# probably did not intend to run GS
alias gs="echo 'Did you mean $(which gst)?'"

# Traversing filesystem
export mnt="/var/run/media/$USER"
function mnt() { cd "$mnt" }
alias p="cd ~/projects"

# Networking
alias https='http --default-scheme=https'
function http-share-dir() {
    if [[ "$1" =~ "--help|-h" ]]; then
        echo "Share a directory over local letwork via HTTP server. Listens on interface 0.0.0.0!"
        echo ""
        echo "Usage: http-share-dir [ <directory> [ <port> ] ]"
        echo ""
        echo "    <directory> - which directory to share, default: current directory"
        echo "    <port>      - on which port server starts, default: 8080"
        return 1
    fi
    local directory=${1:-$PWD}
    local port=${2:-8080}
    python -m http.server \
        "${port}" \
        --bind 0.0.0.0 \
        --directory "${directory}" \
        &                                           # background job
    sleep 1                                         # give server some time to launch
    local url="http://${HOST}.local:${port}/"       # / at the end is important
    echo "Open ${url} in browser"
    xdg-open "${url}"
    fg                                              # bring server to foreground
}

# Local weather
function weather() { curl http://wttr.in/"$1" }
alias inno="weather Иннополис"

# $PATH utils
function :path() { local ptr="${1:-PATH}"; local str="${(P)ptr}"; echo "$str" | tr ':' '\n' }
alias :p=":path"

# Haskell
function ihaskell-notebook() { workon ihaskell && stack exec -- jupyter-notebook --notebook-dir=$HOME/projects/haskell }
alias ghc-find="stack exec -- ghc-pkg find-module --simple-output"

# Nice colorful xxd replacement
type hexyl &> /dev/null && alias xxd="hexyl"

# Clipboard utils
alias xclip="xclip -selection clipboard"
alias xcopy="xclip -in"
alias xpaste="xclip -out"

function any-repl () {
    if [[ $# < 2 ]]; then
        echo "Any REPL - wrap any command in REPL-style interface"
        echo ""
        echo "Usage: $0 prompt cmd [arg ...]"
        echo "    prompt        - Analogue to \$PS1"
        echo "    cmd [arg ...] - Command to run and args to pass. User's input will be passed as the last argument."
        return 1
    fi
    typeset prompt=$1
    shift
    typeset -a cmdargs=($@)
    typeset -g query
    typeset query_prompt='%%%F{green}'$prompt'%f '

    while vared -p $query_prompt -e -c query
    do
        if [[ $query =~ '^\s*$' ]]
        then
            query=""
        else
            $cmdargs $query
        fi
    done
}
function _hoogle-repl-internal() {
    # $1 is a query
    if [[ $1 =~ '^\s*-' ]]; then
        hoogle ${=1} # raw parameters
    else
        hoogle $1    # single string query
    fi
}
alias hoogle-repl="any-repl hoogle _hoogle-repl-internal"

# Docker monitoring
alias sen="docker run -v /var/run/docker.sock:/run/docker.sock -it --rm -e TERM tomastomecek/sen"
alias dps="docker ps"
alias dpsa="docker ps -a"

if type xdg-open &>/dev/null; then
    function open() {
        # Smart alias for xdg-open.
        # Without arguments opens current directory.
        if [ $# = 0 ]; then
            1=.
        fi
        xdg-open "$@"
    }
fi

function installed-size() {
    if [[ "$1" =~ "--help|-h" ]]; then
        echo 'Show total installed size of all packages on Arch Linux system in human-readable format.'
        echo ''
        echo 'Usage: installed-size [units]'
        echo ''
        echo 'Options:'
        echo '  units'
        echo '    (Documentation from `man 1 expac`)'
        echo '    Format packages size in SI units according to `units`. Valid options are:'
        echo '      B, K, M, G, T, P, E, Z, Y'
        echo '    Optional, defaults to `G`.'
        return 1
    fi
    local units="${1:-G}"
    expac -H "$units" '%m' | awk '{sum += $1} END {print sum, $2}'
}
